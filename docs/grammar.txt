-SPACE   = (\s|;|,)*
-COMMENT = --[^\n\r]*

STRING = "[^\"]*"|'[^']*'
FLOAT = -?\d*\.\d+([eE][-+]?\d+)?\b
INT = -?\d+\b
NAME = [a-z]\w*
CONCEPT = [A-Z]\w*

default-path = ':'
default-doc = ':?'
default-format = ':%'
wildcard = '*'

root = expression*

expression = tag | relation | value

tag = '#' name

relation = path sign value
sign = equal | diff | lte | lt | gte | gt | in | out
equal = '='
diff = '!='
lt = '<'
lte = '<='
gt = '>'
gte = '>='
in = '><'
out = '<>'

path = keyword ( sub-path | meta-path )*
sub-path = '/' keyword
meta-path = '.' keyword

keyword = name | concept | log | alias | cache | format | meta | doc
log = '!' name
alias = '@' name
cache = '$' name
format = '%' name
meta = '_' name
doc = '?' name

value = reference | literal | list | object

reference = head-reference sub-reference*
head-reference = query | keyword
sub-reference = '/' (
    range | INT | tag | list | object | query | keyword | wildcard
)

literal = INT | FLOAT | STRING | BOOLEAN

list = '[' value* ']'

range = '..' INT | INT '..' INT?

object = '(' object-key expression* ')'
object-key = path | default-path | default-format | default-doc

query = '{' query-key expression* '}'
query-key = default-path | path

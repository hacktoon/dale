
SPACE = r'(\s|;|,)*'
COMMENT = r'--[^\n\r]*'

string = r'"[^\"]*"|\'[^']*\''
float = r'-?\d*\.\d+([eE][-+]?\d+)?\b'
int = r'-?\d+\b'
name = r'[a-z]\w*'
concept = r'[A-Z]\w*'
anonym = ':'
default-doc = '?:'
default-format = '%:'
wildcard = '*'

root = expression*

expression = tag | relation | value

tag = '#' name

relation = path sign value
sign = equal | diff | lte | lt | gte | gt | in | out
equal = '='
diff = '!='
lt = '<'
lte = '<='
gt = '>'
gte = '>='
in = '><'
out = '<>'

path = keyword ( sub-path | meta-path )*
sub-path = '/' keyword
meta-path = '.' keyword

keyword = name | concept | log | alias | cache | format | meta | doc
log = '!' name
alias = '@' name
cache = '$' name
format = '%' name
meta = '_' name
doc = '?' name

value = reference | literal | list | object

reference = head-reference sub-reference*
head-reference = query | keyword
sub-reference = '/' (
    range | int | tag | list | object | query | keyword | wildcard
)

literal = int | float | string | boolean

list = '[' value* ']'

range = '..' int | int '..' int?

object = '(' object-key expression* ')'
object-key = anonym-path | default-format | default-doc | path

query = '{' query-key expression* '}'
query-key = anonym-path | path

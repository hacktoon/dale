
@root = body?
@space = [SPACE|NEWLINE|,]+

body = expression ( _ expression )*
name = [a-z][a-z|0-9]+
concept = [A-Z][a-z|0-9]*

default-path = ':'
default-doc = '?:'
default-format = '%:'
wildcard = '*'

expression = tag | relation | value

tag = '#' name

relation = path _ sign _ value
sign = equal | diff | lte | lt | gte | gt | in | out
equal = '='
diff = '!='
lt = '<'
lte = '<='
gt = '>'
gte = '>='
in = '><'
out = '<>'

path = keyword ( _ ( sub-path | meta-path ) )*
sub-path = '/' _ keyword
meta-path = '.' _ keyword

keyword = name | concept | log | alias | cache | format | meta | doc
log = '!' name
alias = '@' name
cache = '$' name
format = '%' name
meta = '~' name
doc = '?' name

value = literal | reference | list | object

reference = head-reference sub-reference*
head-reference = query | keyword
sub-reference = _ '/' _ sub-reference-item
sub-reference-item = range | INT | tag | list | object | query | keyword | wildcard

literal = INT | FLOAT | STRING | BOOLEAN

list = '[' _ list-items? _ ']'
list-items = value ( _ value )*

range = '..' INT | INT '..' INT?

object = '(' _ key _ body? _ ')'
key = path | default-path | default-format | default-doc

query = '{' _ target _ body _ '}'
target = default-path | path
